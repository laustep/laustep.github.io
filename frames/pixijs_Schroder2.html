<!doctype html>
<html>

<head>
  <meta charset="utf-8">
  <script src="../libraries/hamster.min.js"></script>
  <title>Schroder fractal</title>
  <style>
    html, body { 
      margin: 0; 
    }
    canvas {
      display: block;
    }
  </style>
</head>
<script src="../libraries/pixi.min.js"></script>

<body>

  <script type="text/javascript">
    let type = "WebGL";
    if (!PIXI.utils.isWebGLSupported()) {
      type = "canvas";
    }
    PIXI.utils.sayHello(type);
  </script>

  <div id="container"></div>

  <script id="fragmentShader" type="x-shader/x-fragment">
    uniform vec2 iResolution;
    uniform float iGlobalTime;
    uniform vec2 mouse;
    uniform bool color;
    uniform float s;

    float lerp(float t, float a, float b){
      return a + t*(b-a);
    }
    float norm(float t, float a, float b){
      return (t-a)/(b-a);
    }
    float map(float t, float e1, float s1, float e2, float s2){
      return lerp(norm(t, e1, s1), e2, s2);
    }

    vec3 viridis(float u){
      return vec3(
        70.0 - 18.0*u + 449.0*u*u - 3461.0*u*u*u + 6058.0*u*u*u*u - 2847.0*u*u*u*u*u, 
        4.0 + 320.0*u - 50.0*u*u - 40.0*u*u*u,
        85.0 + 346.0*u + 119.0*u*u - 5374.0*u*u*u + 15427.0*u*u*u*u - 17627.0*u*u*u*u*u + 7057.0*u*u*u*u*u*u
      ) / 255.0;
    }

    vec2 product(vec2 a, vec2 b){
      return vec2(a.x*b.x-a.y*b.y, a.x*b.y+a.y*b.x);
    }
    vec2 conjugate(vec2 a){
      return vec2(a.x, -a.y);
    }
    vec2 divide(vec2 a, vec2 b){
      return vec2(a.x*b.x+a.y*b.y, a.y*b.x-a.x*b.y) / (b.x*b.x+b.y*b.y);
    }

    float pi = 3.1415926535897932384626433832795;
    float pio2 = pi/2.0;

    vec2 mobius(vec2 z, vec2 gamma, float t){
      float g2 = gamma.x*gamma.x + gamma.y*gamma.y;
      float h = sqrt(1.0 - g2);
      vec2 d2 = pow(h, t) * vec2(cos(t*pio2), sin(t*pio2));
      vec2 d1 = conjugate(d2);
      vec2 a = vec2(d1.x, -d1.y/h);
      vec2 b = d2.y * gamma / h;
      vec2 c = conjugate(b);
      vec2 d = conjugate(a);
      return divide(product(a, z) + b, product(c, z) + d);
    }

    vec3 hsv2rgb(vec3 c){
      vec4 K = vec4(1.0, 2.0/3.0, 1.0/3.0, 3.0);
      vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
      return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
    }

    vec3 pair2hsv(vec2 z){
      float h = (atan(z.y, z.x) + pi) / 2.0 / pi;
      float l = sqrt(z.x*z.x + z.y*z.y);
      float s = (1.0 + sin(2.0*pi*log(1.0+l))) / 2.0;
      float v = (1.0 + cos(2.0*pi*log(1.0+l))) / 2.0;
      return vec3(h, s, v);
    }

    vec2 f(vec2 z){
      float re = z.x*z.x*z.x - 3.0*z.x*z.y*z.y;
      float im = -z.y*z.y*z.y + 3.0*z.x*z.x*z.y;
      vec2 z3 = vec2(re, im);
      return z3-1.0;
    }

    vec2 df(vec2 z){
      float re = z.x*z.x - z.y*z.y;
      float im = 2.0*z.x*z.y;
      vec2 z2 = vec2(re, im);
      return 3.0*z2;
    }

    vec2 ddf(vec2 z){
      return 6.0*z;
    }

    void main(void){
      vec2 z0 = vec2(
        map(gl_FragCoord.x, 0.0, iResolution.x, -10.0, 10.0),
        map(gl_FragCoord.y, 0.0, iResolution.y, -10.0, 10.0)
      );
      vec2 z = s * mobius(z0/s, vec2(0.01,0.02), 1.0 + cos(iGlobalTime));
      float ii;
      vec2 fz = f(z); 
      for(int i = 0; i < 12; i++){
        vec2 dfz = df(z);
        z = z - divide(product(fz,dfz), product(dfz,dfz) - product(fz,ddf(z)));
        ii = float(i);
        fz = f(z);
        if(fz.x*fz.x + fz.y*fz.y < 0.000000001){
          break;
        }
      }
      vec2 w = ii == 11.0 ? divide(vec2(1.0,0.0),fz) : 1000000000.0*fz;
      gl_FragColor = color ? vec4(hsv2rgb(pair2hsv(w)), 1.0) : vec4(vec3(ii/11.0), 1.0);
    }
  </script>

  <script>
    var app = new PIXI.Application({ width: window.innerWidth, height: window.innerHeight });
    app.resizeTo = window;

    let fragmentShader = document.getElementById('fragmentShader').textContent;

    let filter = new PIXI.Filter(null, fragmentShader);
    filter.uniforms.iResolution = [app.screen.width, app.screen.height];
    filter.uniforms.iGlobalTime = 0;
    filter.uniforms.mouse = { x: 0, y: 0 }; 
    filter.uniforms.color = false;
    filter.uniforms.s = 1;

    document.onmousemove = function(evt){
      filter.uniforms.mouse = { x: evt.clientX, y: evt.clientY };
    };

    document.onclick = function(evt){
      filter.uniforms.color = !filter.uniforms.color;
    };

    let factor0 = 1.001;
    let hamster = Hamster(document.body);
    hamster.wheel(function (event, delta, deltaX, deltaY) {
      let factor = Math.max(0.1, Math.pow(factor0, deltaY));
      filter.uniforms.s /= factor;
    });

    let container = new PIXI.Container();
    container.filterArea = app.screen;
    container.filters = [filter];

    app.stage.addChild(container);
    document.getElementById('container').appendChild(app.view);

    function onresize(event){
      if(app.resize)
        app.resize();
      container.filterArea = app.screen;
      filter.uniforms.iResolution = [app.screen.width, app.screen.height];
    }
    window.addEventListener('resize', onresize, false);

    let startTime = Date.now();
    app.ticker.add(function (delta) {
      var currentTime = Date.now();
      filter.uniforms.iGlobalTime = (currentTime - startTime) * 0.00025;
    });

  </script>
</body>

</html>
